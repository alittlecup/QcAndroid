apply plugin: 'com.android.application'
apply from: rootProject.file('qc_component.gradle')

def libs = rootProject.ext.libraries
def cfg = rootProject.ext.configuration
def dependens = rootProject.ext.dependencies
def annotations = rootProject.ext.annotations

def getDate() {
  def date = new Date()
  def formattedDate = date.format('yyyyMMddHHmm')
  return formattedDate
}

android {
  compileSdkVersion cfg.compileVersion
  buildToolsVersion cfg.buildToolsVersion

  defaultConfig {
    applicationId "com.qingchengfit.fitcoach"
    minSdkVersion 15
    targetSdkVersion 26
    multiDexEnabled true
    versionCode cfg.trainer_version_code
    versionName cfg.trainer_version_name
    vectorDrawables.useSupportLibrary = true
    javaCompileOptions {
      annotationProcessorOptions {
        arguments = [packageName: 'cn.qingchengfit.staff',]
      }
    }
  }
  dataBinding {
    enabled true
  }
  useLibrary 'org.apache.http.legacy'



  signingConfigs {
    release {
      storeFile file("../qingcheng.keystore")
      storePassword "Qingcheng2014"
      keyAlias "Qingcheng"
      keyPassword "Qingcheng2014"
    }
  }
  dexOptions {
    javaMaxHeapSize "8g"
  }
  lintOptions {
    abortOnError false
  }

  flavorDimensions "qc"
  productFlavors {
    dev {
      minSdkVersion 21
      applicationId "com.qingchengfit.fitcoach.dev"
      manifestPlaceholders = [APP_NAME            : "内部测试"
                              , APP_SCHAME        : "qccoach"
                              , bdpush_authorities: "com.qingchengfit.fitcoach.dev.bdpush"
                              , APP_PACKAGE       : "com.qingchengfit.fitcoach.dev"
                              , file_provider     : "com.qingchengfit.fitcoach.dev.provider"

      ]
      ndk {
        abiFilter "armeabi-v7a"
      }
      dimension 'qc'
    }
    product {
      minSdkVersion 16
      applicationId "com.qingchengfit.fitcoach"
      manifestPlaceholders = [APP_NAME            : "青橙健身教练"
                              , APP_SCHAME        : "qccoach"
                              , bdpush_authorities: "com.qingchengfit.fitcoach.bdpush"
                              , APP_PACKAGE       : "com.qingchengfit.fitcoach"
                              , file_provider     : "com.qingchengfit.fitcoach.provider"]
      ndk {
        abiFilter "armeabi-v7a"
      }

      dimension 'qc'
    }
    //动态修改配置
    applicationVariants.all { variant ->
      if (variant.buildType.name == "release") {
        println(rootProject.ext.today)
        variant.outputs.all { output ->
          output.setVersionNameOverride("1." + rootProject.ext.today)
          output.setVersionCodeOverride(rootProject.ext.today)
        }
      }
    }
  }

  buildTypes {
    release {
      signingConfig signingConfigs.release
      minifyEnabled true
      debuggable false
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
    }
    debug {
      signingConfig signingConfigs.release
      debuggable true
    }
  }
  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }
  configurations.all {
    resolutionStrategy.force "com.google.code.findbugs:jsr305:3.0.1"
    resolutionStrategy.force "com.android.support:support-v4:${libs.supportVersion}"
    resolutionStrategy.force "com.android.support:support-v13:${libs.supportVersion}"
    resolutionStrategy.force "com.android.support:support-annotations:${libs.supportVersion}"
    resolutionStrategy.force "com.android.support:appcompat-v7:${libs.supportVersion}"
    resolutionStrategy.force "com.android.support:appcompat-v7:${libs.supportVersion}"
    resolutionStrategy.force "com.android.support:cardview-v7:${libs.supportVersion}"
    resolutionStrategy.force "com.android.support:design:${libs.supportVersion}"
    resolutionStrategy.force "com.android.support:percent:${libs.supportVersion}"
  }

}

//自动加固
afterEvaluate {
  android.applicationVariants.all { variant ->
    variant.assemble.doLast {
      if (variant.buildType.name.contains('release')) {
        variant.outputs.all {
          output ->
            def apkpath = output.outputFile as File
            exec {
              ignoreExitValue true
              commandLine "sh", rootProject.file('reinforce.sh').absolutePath, apkpath, rootProject.file('reinforce.sh').getParentFile().absolutePath,"trainer"
            }
        }

      }
    }
  }
}

dependencies {
  addComponent('saasbase',false)
  addComponent('login',false)
  addComponent('card',false)
  addComponent('student',false)
  addComponent('checkout',false)



  implementation 'cn.qingchegfit.widgets:weekview:1.1@aar'

  implementation fileTree(include: ['*.jar'], dir: 'libs')
  implementation files('libs/pushservice-5.2.0.12.jar')

  implementation 'uk.co.chrisjenx:calligraphy:2.1.0'

  implementation 'com.github.badoualy:stepper-indicator:1.0.6'

  implementation dependens["wechat"]
  implementation dependens["taglib"]
  implementation dependens["floatingactionbutton"]


  annotationProcessor annotations["dagger-compiler"]
  annotationProcessor annotations["dagger-android-processor"]


  annotationProcessor annotations["flabellum-dagger2"]
  annotationProcessor annotations["flabellum-processor"]

  annotationProcessor annotations["fragmentargs"]

  annotationProcessor annotations["room"]

}
apply plugin: 'auto-register'
autoregister {
  registerInfo = [[//自动注册组件
                   'scanInterface'          : 'cn.qingchengfit.router.IComponent'
                   , 'codeInsertToClassName': 'cn.qingchengfit.router.ComponentManager'
                   , 'registerMethodName'   : 'registerComponent'
                   , 'exclude'              : [//排除的类，支持正则表达式（包分隔符需要用/表示，不能用.）
                                               'cn.qingchengfit.router.'.replaceAll("\\.",
                                                   "/") + ".*"]],
                  [//自动注册全局拦截器
                   'scanInterface'          : 'cn.qingchengfit.router.IGlobalCCInterceptor'
                   , 'codeInsertToClassName': 'cn.qingchengfit.router.GlobalCCInterceptorManager'
                   , 'registerMethodName'   : 'registerGlobalInterceptor'
                   , 'exclude'              : [//排除的类，支持正则表达式（包分隔符需要用/表示，不能用.）
                                               'cn.qingchengfit.router.'.
                                                   replaceAll("\\.",
                                                       "/") + ".*"]],
                  [//自动注册跨进程通信时自定义类型的json转换器，可以用Gson、FastJson等工具来实现
                   // 参考： demo_base/src/main/java/com.billy.cc.demo.base.GsonParamConverter
                   'scanInterface'          : 'cn.qingchengfit.router.IParamJsonConverter'
                   , 'codeInsertToClassName': 'cn.qingchengfit.router.RemoteParamUtil'
                   , 'registerMethodName'   : 'initRemoteCCParamJsonConverter'
                   , 'exclude'              : [//排除的类，支持正则表达式（包分隔符需要用/表示，不能用.）
                                               'cn.qingchengfit.router.'.
                                                   replaceAll("\\.",
                                                       "/") + ".*"]]]
}

